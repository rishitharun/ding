Sample Script to send an IP4 PACKET

 * We will specify the transport layer protocol as UDP
 * But the UDP packet will not be in packet format
 * The UDP packet will be given in the stream format
 * Payload for UDP will be a Normal Textual String
 * It will be attached to the UDP Packet using cat() function
 * You can check the IP4 (with UDP) Packet in Wireshark

.decl:
  packet IP4, ETH                  ! IP4 and Ether packets are declared
  stream udp_header, ip4_payload   ! UDP header as stream, for IP4 payload and IP4 payload stream
  string udp_payload               ! String Data for UDP payload

.func:
  ! Block in which user defined functions are written
  ! User Defined Function feature is not yet developed/implemented
  ! Any Code inside this block will be ignored by the interpreter

.flow:

  ETH = container("ETHER")
  IP4 = container("IP4")

  ! Empty Containers for the packets are created
  ! The packet structures have been defined

  udp_header = x08000806002d0000
  udp_payload = "Hello ! This is UDP Payload String !"

  ! UDP header stream is defined
  ! UDP Payload is defined

  ip4_payload = cat(udp_header, udp_payload)

  ! IP4 payload is generated by concatenating UDP header stream and UDP payload string

  IP4.VERSION = 4:4b
  IP4.HEADER_LEN = 5:4b
  IP4.TYPE_OF_SERVICE =12:1B
  IP4.TOTAL_LEN =65:2B
  IP4.IDENTIFICATION=300:2B

  IP4.FLAGS = 0:3b
  IP4.FRAG_OFFSET = 0:13b

  IP4.TTL= 200:1B
  IP4.PROTOCOL = 17:1B
  IP4.CHECKSUM = xfd84
  IP4.SRC_IP = 255.254.253.252
  IP4.DEST_IP = 251.250.249.248
  IP4.PAYLOAD = ip4_payload

  ! IP4 Packet has been defined

  ETH.DEST_MAC = ff~dd~ee~cc~bb~aa
  ETH.SRC_MAC = 11~22~33~44~55~66
  ETH.TYPE = 2048:2B

  ! Ether Packet has been defined

  IP4.PAYLOAD = ip4_payload
  ETH.PAYLOAD = IP4

  ! IP4 Packet is made as the Ether Packet's payload

  interface("lo")
 
  ! Loopback interface is selected

  send(ETH)   ! Sending the Packet

  print("IP4 Packet Sent Successfully")

